plugins {
    id "com.jfrog.bintray" version "1.7"
    id "com.github.dcendents.android-maven" version "1.5"
}
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext {
    PUBLISH_GROUP_ID = 'no.nordli'
    PUBLISH_ARTIFACT_ID = 'eventadapter'
    PUBLISH_VERSION = '0.9'
    PUBLISH_DESCRIPTION = 'Pre-release to test push'
}

version = PUBLISH_VERSION
group = PUBLISH_GROUP_ID

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName PUBLISH_VERSION
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'EventAdapter'
            description 'An event-based recyclerAdapter using an eventBus to notify it of any changes in the datasource, letting the adapter show RecyclerViews nice animations (or you could add your own animations).'
            url 'https://github.com/brynjen/eventadapter'
            inceptionYear '2016'

            packaging 'aar'
            groupId PUBLISH_GROUP_ID
            artifactId 'library'
            version PUBLISH_VERSION

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/brynjen/eventadapter.git'
                url 'https://github.com/brynjen/eventadapter'

            }
            developers {
                developer {
                    name 'Brynje Nordli'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('bintrayUser')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('bintrayKey')
    configurations = ['archives']
    pkg {
        repo = 'no.nordli'
        name = PUBLISH_ARTIFACT_ID
        licenses = ['Apache-2.0']
        userOrg = user
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        version {
            name = PUBLISH_VERSION
            desc = PUBLISH_DESCRIPTION
            vcsTag = '0.1'
            attributes = ['gradle-plugin': 'com.android.tools.build:gradle:2.1.3'] //'com.use.less:com.use.less.gradle:gradle-useless-plugin'
        }
    }
}