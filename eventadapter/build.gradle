plugins {
    id "com.jfrog.bintray" version "1.7"
    id "com.github.dcendents.android-maven" version "1.5"
}
apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

ext {
    PUBLISH_GROUP_ID = 'no.nordli'
    PUBLISH_ARTIFACT_ID = 'eventadapter'
    PUBLISH_VERSION = '0.9'
    PUBLISH_DESCRIPTION = 'Pre-release to test push'
    GIT_REPO_URL = 'https://github.com/brynjen/eventadapter'
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName PUBLISH_VERSION
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    javadocDeps2
    javadocDeps
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'

    javadocDeps 'com.android.support:support-annotations:23.4.0'
    javadocDeps2 'com.android.support:recyclerview-v7:23.4.0'//''com.android.support:widget-v7:23.4.0'
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'EventAdapter'
            description 'An event-based recyclerAdapter using an eventBus to notify it of any changes in the datasource, letting the adapter show RecyclerViews nice animations (or you could add your own animations).'
            url GIT_REPO_URL
            inceptionYear '2016'

            packaging 'aar'
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection GIT_REPO_URL+'.git'//'https://github.com/brynjen/eventadapter.git'
                url GIT_REPO_URL

            }
            developers {
                developer {
                    name 'Brynje Nordli'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps2
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('bintrayUser')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('bintrayKey')
    configurations = ['archives']
    pkg {
        repo = PUBLISH_GROUP_ID //  Bintray repo, corresponding with groupId here
        name = PUBLISH_ARTIFACT_ID // This is package name
        desc = PUBLISH_DESCRIPTION
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        userOrg = user
        vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
        githubRepo = GIT_REPO_URL
        version {
//            name = PUBLISH_ARTIFACT_ID // This is package name
            publish = true
            released = new Date()
            attributes = ['gradle-plugin': 'com.android.tools.build:gradle:2.2.0'] //'com.use.less:com.use.less.gradle:gradle-useless-plugin'
        }
    }
}