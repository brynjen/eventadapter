apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

ext {
    PUBLISH_GROUP_ID = 'no.nordli'
    PUBLISH_ARTIFACT_ID = 'eventadapter'
    PUBLISH_VERSION = '1.2.8'
    PUBLISH_VERSION_CODE = 10
    GIT_REPO_URL = 'https://github.com/brynjen/eventadapter'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode PUBLISH_VERSION_CODE
        versionName PUBLISH_VERSION
        testHandleProfiling false
        testFunctionalTest false
    }
    buildTypes {
        debug {
            testCoverageEnabled false
        }
        release {
            testCoverageEnabled false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    javadocDeps2
    javadocDeps
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    javadocDeps 'com.android.support:support-annotations:25.3.1'
    javadocDeps2 'com.android.support:recyclerview-v7:25.3.1'//''com.android.support:widget-v7:23.4.0'
}

publish {
    userOrg = 'brynjen'
    repoName = 'no.nordli'
    groupId = 'no.nordli'
    artifactId = PUBLISH_ARTIFACT_ID
    publishVersion = PUBLISH_VERSION
    bintrayUser = properties["bintrayUser"]
    bintrayKey = properties["bintrayKey"]
    dryRun = false
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc, dependsOn:'assembleRelease') {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps2
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}